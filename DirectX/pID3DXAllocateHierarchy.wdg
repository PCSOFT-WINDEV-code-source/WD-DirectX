#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : pID3DXAllocateHierarchy
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x19e5b2dc116e05ba
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  type_code : 31
  p_codes :
   -
     internal_properties : CAAAAAgAAACrjc2Vo5nnxtHat7jk7y5/+QuO/FY3pzOhWPM+eFVJUUtegCANqt2R0plvZnTKw7zJPLjk5gqleQEQuyzBn2uHLsbnCtrXB0lrJdXDL3lNVdROFx6zavmY2/uIY0CoCPPyrnI4V9d943P2F0w=
     code : |1-
      
      // Cette collection de procédures est l'interface de rebond pour les objets ID3DXAllocateHierarchy
      // Elle sert de relai entre DirectX et la classe WLangage ID3DXAllocateHierarchy
      // La classe ID3DXAllocateHierarchy doit être redéfinie et ses méthodes surchargées
      
      __ID3DXAllocateHierarchyObjects est un tableau associatif (*, *, wlentier) de ID3DXAllocateHierarchy dynamique
      
      // NE MODIFIEZ PAS LES PROTOTYPES DES FONCTIONS CI-DESSOUS : il en résulterait des comportements indéfinis (plantages, blocages, ...)
     type : 720896
  procedures :
   -
     name : ID3DXAllocateHierarchy_RegisterObject
     procedure_id : 1866094313231230018
     type_code : 15
     code : |1-
      // Résumé : Fonction RegisterObject
      Procédure ID3DXAllocateHierarchy_RegisterObject(pclID3DXAllocateHierarchy est un ID3DXAllocateHierarchy dynamique)
      
      	// Enregistre l'objet
      	__ID3DXAllocateHierarchyObjects[&pclID3DXAllocateHierarchy] = pclID3DXAllocateHierarchy
      	
      	// Enregistre les méthodes de l'objet
      	// NE MODIFIEZ PAS L'ORDRE D'ENREGISTREMENT DES METHODES
      	pclID3DXAllocateHierarchy:__RegisterMethod(&ID3DXAllocateHierarchy_CreateFrame)
      	pclID3DXAllocateHierarchy:__RegisterMethod(&ID3DXAllocateHierarchy_CreateMeshContainer)
      	pclID3DXAllocateHierarchy:__RegisterMethod(&ID3DXAllocateHierarchy_DestroyFrame)
      	pclID3DXAllocateHierarchy:__RegisterMethod(&ID3DXAllocateHierarchy_DestroyMeshContainer)
      	
      	// Enregistre la VTABLE
      	pclID3DXAllocateHierarchy:__SaveVTABLE()
     type : 458752
   -
     name : ID3DXAllocateHierarchy_CreateFrame
     procedure_id : 1866096804312645242
     type_code : 15
     code : |1-
      // Résumé : Méthode virtuelle CreateFrame
      Procédure privee ID3DXAllocateHierarchy_CreateFrame(_THIS_, pName est un entier systeme, pNewFrame est un entier systeme)
      
      	HRESULT est un entier systeme
      
      	// Si on ne trouve pas l'objet
      	SI __ID3DXAllocateHierarchyObjects[_THIS_] = Null ALORS
      		
      		// Renvoie E_NOTIMPL
      		RENVOYER E_NOTIMPL
      	SINON
      		
      		sName 		est une chaine 	= chainerecupere(pName, crAdresseASCIIZ)
      		stNewFrame 	est un d3dxframe
      		
      		// Appel de la méthode
      		HRESULT = __ID3DXAllocateHierarchyObjects[_THIS_]:CreateFrame(sName, stNewFrame)
      		
      		// Transfert
      		Transfert(pNewFrame, &stNewFrame, Dimension(stNewFrame))		
      		renvoyer HRESULT
      	FIN
     type : 458752
   -
     name : ID3DXAllocateHierarchy_CreateMeshContainer
     procedure_id : 1866096804312710825
     type_code : 15
     code : |1+
      // Résumé : Méthode virtuelle CreateMeshContainer
      Procédure PRIVÉE ID3DXAllocateHierarchy_CreateMeshContainer(_THIS_, pName est un entier systeme, pMeshData est un entier systeme, pMaterials est un entier systeme, pEffectInstances est un entier systeme, NumMaterials est un entier sans signe, pAdjacency est un entier systeme, pSkinInfo est un entier systeme, ppNewMeshContainer est un entier systeme)
      
      	HRESULT est un entier systeme
      	stMaterial est un D3DXMATERIAL
      	stEffect est un D3DXEFFECTINSTANCE
      	
      	// Si on ne trouve pas l'objet
      	SI __ID3DXAllocateHierarchyObjects[_THIS_] = Null ALORS
      		
      		// Renvoie E_NOTIMPL
      		RENVOYER E_NOTIMPL
      	SINON
      		
      		sName 				est une chaîne 	= ChaîneRécupère(pName, crAdresseASCIIZ)
      		stMeshData			est un D3DXMESHDATA
      		tabMaterials		est un tableau de NumMaterials D3DXMATERIAL
      		tabEffectInstances	est un tableau de NumMaterials D3DXEFFECTINSTANCE
      		tabAdjacency		est un tableau de NumMaterials entiers sans signe
      		pclSkinInfo			est un ID3DXSkinInfo dynamique
      		stNewMeshContainer	est un D3DXMESHCONTAINER
      		
      		// Récupère les informations
      		transfert(&stMeshData, 			pMeshData, dimension(stMeshData))
      		transfert(&tabMaterials, 		pMaterials, NumMaterials * dimension(stMaterial))
      		transfert(&tabEffectInstances, 	pEffectInstances, NumMaterials * dimension(stEffect))
      		transfert(&tabAdjacency, 		pAdjacency, NumMaterials * 4)
      		
      		__BindInterface(pclSkinInfo, pSkinInfo, ID3DXSkinInfo)		
      		
      		// Appel de la méthode
      		HRESULT = __ID3DXAllocateHierarchyObjects[_THIS_]:CreateMeshContainer(sName, stMeshData, tabMaterials, tabEffectInstances, NumMaterials, tabAdjacency, pclSkinInfo, stNewMeshContainer)			
      		
      		// Transfert
      		transfert(ppNewMeshContainer, ptr(stNewMeshContainer), dimension(stNewMeshContainer))	
      		RENVOYER HRESULT		
      	FIN
     type : 458752
   -
     name : ID3DXAllocateHierarchy_DestroyFrame
     procedure_id : 1866096804312776392
     type_code : 15
     code : |1-
      // Résumé : Méthode virtuelle DestroyFrame
      Procédure PRIVÉE ID3DXAllocateHierarchy_DestroyFrame(_THIS_, pFrameToFree est un entier systeme)
      
      	// Si on ne trouve pas l'objet
      	SI __ID3DXAllocateHierarchyObjects[_THIS_] = Null ALORS
      		
      		// Renvoie E_NOTIMPL
      		RENVOYER E_NOTIMPL
      	SINON
      		
      		stFrame 	est un d3dxframe
      		transfert(&stFrame, pFrameToFree, dimension(stFrame))
      		
      		// Appel de la méthode
      		renvoyer __ID3DXAllocateHierarchyObjects[_THIS_]:DestroyFrame(stFrame)
      	FIN
     type : 458752
   -
     name : ID3DXAllocateHierarchy_DestroyMeshContainer
     procedure_id : 1866096804312841975
     type_code : 15
     code : |1-
      // Résumé : Méthode virtuelle DestroyMeshContainer
      Procédure PRIVÉE ID3DXAllocateHierarchy_DestroyMeshContainer(_THIS_, pMeshContainerToFree est un entier systeme)
      
      	// Si on ne trouve pas l'objet
      	SI __ID3DXAllocateHierarchyObjects[_THIS_] = Null ALORS
      		
      		// Renvoie E_NOTIMPL
      		RENVOYER E_NOTIMPL
      	SINON
      		
      		stContainer 	est un D3DXMESHCONTAINER
      		Transfert(&stContainer, pMeshContainerToFree, Dimension(stContainer))
      		
      		// Appel de la méthode
      		RENVOYER __ID3DXAllocateHierarchyObjects[_THIS_]:DestroyMeshContainer(stContainer)
      	FIN
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_Collection1
resources :
 string_res :
  identifier : 0x10d999010f80b703
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
